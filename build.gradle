plugins {
    // Apply the java plugin to add support for Java
    id 'java'

    // Apply the application plugin to add support for building a CLI application.
    id 'application'

    //Apply the idea plugin to add support for Intellij IDEA
    id 'idea'

    //Apply the JavaFX plugin to add support for JavaFX libraries
    id 'org.openjfx.javafxplugin' version '0.0.8'

    //Apply the shadow plugin to add support for creating a fat jar
    id "com.github.johnrengelman.shadow" version "5.2.0"

    //Apply the ANTLR java parser generator plugin
    id 'antlr'
}

repositories {
    // Use jcenter for resolving dependencies.
    jcenter()
}

dependencies {
    // This dependency is used by the application.
    implementation 'com.google.guava:guava:28.2-jre'

    // Java parser generator
    antlr "org.antlr:antlr4:4.7.2"

    // Use JUnit test framework
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.4.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.4.2'

    // Add support for Efficient Java Matrix Library
    implementation (group: 'org.ejml', name: 'ejml-simple', version: '0.39'){
        exclude group: 'org.ejml', module: 'ejml-fdense'
        exclude group: 'org.ejml', module: 'ejml-zdense'
        exclude group: 'org.ejml', module: 'ejml-cdense'
        exclude group: 'org.ejml', module: 'ejml-dsparse'
        exclude group: 'org.ejml', module: 'ejml-csparse'
    }


}

generateGrammarSource {
    outputDirectory = file("src/main/java/LogicCircuitSimulator/BooleanExpressionParser/GrammarParser")
    arguments += ["-visitor", "-long-messages"]
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

application {
    // Define the main class for the application.
    mainClassName = 'LogicCircuitSimulatorModule/LogicCircuitSimulator.Launcher'
}

javafx{
    version = "11.0.2"
    modules = ['javafx.controls', 'javafx.fxml']
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}